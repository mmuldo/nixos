# THIS IS CONFIGURATION 2
{ config, lib, pkgs, inputs, system, user, modulesPath, ... }:

{
  imports = [
    inputs.home-manager.nixosModules.default
    "${toString modulesPath}/profiles/qemu-guest.nix"

    ../../modules/nixos/user.nix
  ];

  nix.settings.experimental-features = [ "nix-command" "flakes" ];
  nixpkgs.hostPlatform = system;

  home-manager = {
    extraSpecialArgs = {
      inherit inputs;
      inherit user;
    };
    users.${user.name} = import ./home.nix;
  };

  environment.systemPackages = with pkgs; [
    vim
    wget
    git
    ripgrep
    gcc
    inetutils
    mtr
    sysstat
    linode-cli
  ];

  users.users.root.openssh.authorizedKeys.keys = user.ssh.authorizedKeys;

  services.openssh = {
    enable = true;
    settings = {
      PasswordAuthentication = false;
      # PermitRootLogin = "no";
    };
  };

  # Do NOT change this value unless you have manually inspected all the changes it would make to your configuration,
  # and migrated your data accordingly.
  #
  # For more information, see `man configuration.nix` or https://nixos.org/manual/nixos/stable/options#opt-system.stateVersion .
  system.stateVersion = "23.11"; # Did you read the comment?

  # Set up filesystems according to Linode preference:
  fileSystems."/" = {
    device = "/dev/sda";
    fsType = "ext4";
    autoResize = true;
  };

  swapDevices = [{device = "/dev/sdb";}];

  # Enable LISH and Linode booting w/ GRUB
  boot = {
    # Add kernel modules detected by nixos-generate-config:
    initrd.availableKernelModules = [
      "virtio_pci"
      "virtio_scsi"
      "ahci"
      "sd_mod"
    ];

    growPartition = true;

    # Set up LISH serial connection:
    kernelParams = ["console=ttyS0,19200n8"];

    loader = {
      # Increase timeout to allow LISH connection:
      timeout = lib.mkForce 10;

      grub = {
        enable = true;
        forceInstall = true;
        device = "nodev";
        fsIdentifier = "label";

        # Allow serial connection for GRUB to be able to use LISH:
        extraConfig = ''
          serial --speed=19200 --unit=0 --word=8 --parity=no --stop=1;
          terminal_input serial;
          terminal_output serial
        '';

        # Link /boot/grub2 to /boot/grub:
        extraInstallCommands = ''
          ${pkgs.coreutils}/bin/ln -fs /boot/grub /boot/grub2
        '';

        # Remove GRUB splash image:
        splashImage = null;
      };
    };
  };

  # Hardware option detected by nixos-generate-config:
  hardware.cpu.amd.updateMicrocode = lib.mkDefault config.hardware.enableRedistributableFirmware;

  networking = {
    enableIPv6 = true;
    tempAddresses = "disabled";
    useDHCP = true;
    usePredictableInterfaceNames = false;
    interfaces.eth0 = {
      tempAddress = "disabled";
      useDHCP = true;
    };

    nat = {
      enable = true;
      enableIPv6 = true;
      externalInterface = "eth0";
      internalInterfaces = [ "wg0" ];
    };

    firewall = {
      allowedTCPPorts = [ 22 80 443 53 49640 ];
      allowedUDPPorts = [ 53 80 443 51820 49640 ];
    };

    wg-quick.interfaces = {
      wg0 = {
        address = [ "10.0.0.1/24" ];
        listenPort = 51820;
        privateKeyFile = "/root/wireguard-keys/private.key";

        postUp = ''
          ${pkgs.iptables}/bin/iptables -A FORWARD -i wg0 -j ACCEPT
          ${pkgs.iptables}/bin/iptables -A FORWARD -o wg0 -j ACCEPT
          ${pkgs.iptables}/bin/iptables -t nat -A POSTROUTING -o eth0 -j MASQUERADE
        '';
        preDown = ''
          ${pkgs.iptables}/bin/iptables -D FORWARD -i wg0 -j ACCEPT
          ${pkgs.iptables}/bin/iptables -D FORWARD -o wg0 -j ACCEPT
          ${pkgs.iptables}/bin/iptables -t nat -D POSTROUTING -o eth0 -j MASQUERADE
        '';

        peers = [
          {
            publicKey = "aEL1w5CRjzUXfSQB17lL6xa+CbGSR7VuOfGjSgG9JA4=";
            allowedIPs = [ "10.0.0.2/32" ];
          }

          {
            publicKey = "xE/QHqr/qvvsG8EWLTqkFrfSWcZhep03CFgtW3w+mVo=";
            allowedIPs = [ "10.0.0.3/32" ];
          }
        ];
      };
    };
  };

  services = {
    qemuGuest.enable = true;
    dnsmasq = {
      enable = true;
      extraConfig = ''
        interface=wg0
      '';
    };
  };

}

